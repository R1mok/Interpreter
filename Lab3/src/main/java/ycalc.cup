/*
  This example comes from a short article series in the Linux 
  Gazette by Richard A. Sevenich and Christopher Lopes, titled
  "Compiler Construction Tools". The article series starts at

  http://www.linuxgazette.com/issue39/sevenich.html

  Small changes and updates to newest JFlex+Cup versions 
  by Gerwin Klein
*/

/*
  Commented By: Christopher Lopes
  File Name: ycalc.cup
  To Create: > java java_cup.Main < ycalc.cup
*/
   
/* ----------------------Preliminary Declarations Section--------------------*/
   
/* Import the class java_cup.runtime.*  */
import java_cup.runtime.*;
import javafx.util.Pair;
import java.util.HashSet;
import java.util.Set;
import java.util.Objects;
import java.util.LinkedList;
import java.io.*;
import java.util.Arrays;
/* Parser code to change the way the parser reports errors (include
   line and column number of the error). */
parser code {:
    /** User initialization code. */
    VarFunctionsContext context = new VarFunctionsContext();
    public static void newScope(){
	typenames.push(new HashSet<String>());
    }
    public static void deleteScope(){
	typenames.pop();
    }
    public static boolean lookupType(String name){
	for (HashSet<String> scope: typenames)
	    if (scope.contains(name)) return true;
	return false;
    }
    public static void addType(String name){
        typenames.peek().add(name);
    }
    Set<Pair<String, Integer>> varArray = new HashSet();
    public static LinkedList<HashSet<String>> typenames = new LinkedList<HashSet<String>>();
    public Parser(Lexer lex, ComplexSymbolFactory sf) {
        super(lex,sf);
    }
    /* Change the method report_error so it will display the line and
       column of where the error occurred in the input as well as the
       reason for the error which is passed into the method in the
       String 'message'. */
    public void report_error(String message, Object info) {
   
        /* Create a StringBuilder called 'm' with the string 'Error' in it. */
        StringBuilder m = new StringBuilder("Error");
   
        /* Check if the information passed to the method is the same
           type as the type java_cup.runtime.Symbol. */
        if (info instanceof java_cup.runtime.Symbol) {
            /* Declare a java_cup.runtime.Symbol object 's' with the
               information in the object info that is being typecasted
               as a java_cup.runtime.Symbol object. */
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);
   
            /* Check if the line number in the input is greater or
               equal to zero. */
            if (s.left >= 0) {                
                /* Add to the end of the StringBuilder error message
                   the line number of the error in the input. */
                m.append(" in line "+(s.left+1));   
                /* Check if the column number in the input is greater
                   or equal to zero. */
                if (s.right >= 0)                    
                    /* Add to the end of the StringBuilder error message
                       the column number of the error in the input. */
                    m.append(", column "+(s.right+1));
            }
        }
   
        /* Add to the end of the StringBuilder error message created in
           this method the message that was passed into this method. */
        m.append(" : "+message);
   
        /* Print the contents of the StringBuilder 'm', which contains
           an error message, out on a line. */
        System.err.println(m);
    }

   public static class Main {
     static public void main(String argv[]) {
       /* Start the parser */
       try {
         Parser p = new Parser(new Lexer(new FileReader(argv[0])));
         System.out.println("Start parsing");
         p.context.newScope();
         Program result = (Program) p.parse().value;
         p.context.deleteScope();
         System.out.println(p.context.ex(p.context.rootFunc("main")));
         p.context.getFunctions();
         p.context.getVariables();
         System.out.println("Finish parsing");
       } catch (Exception e) {
         /* do cleanup here -- possibly rethrow e */
         System.out.println("");
         e.printStackTrace();
       }
     }
   }
    /* Change the method report_fatal_error so when it reports a fatal
       error it will display the line and column number of where the
       fatal error occurred in the input as well as the reason for the
       fatal error which is passed into the method in the object
       'message' and then exit.*/
    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
        System.exit(1);
    }
:};
   

   
/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
/* Terminals (tokens returned by the scanner).  

   Terminals that have no value are listed first and then terminals
   that do have an value, in this case an integer value, are listed on
   the next line down. */

terminal SEMI, PLUS, MINUS, TIMES, DIVIDE, LPAREN, RPAREN, ASSIGN, COMMA, MOD, GTE, LTE, NE; // arythmetical terminals
terminal Integer NUMBER; // int terminals
terminal String LBRACE, RBRACE, LSQUARE, RSQUARE, WHILE; // other string terminals
terminal String POINTER, VALUE, NAME, CONST, ARRAY_OF; // type terminals

non terminal Opr initialize, initialize_part;
non terminal Program    programm;
non terminal Types      type, value, const_value;
non terminal Types      pointer, array_of, const_pointer, const_array_of;
non terminal Opr   function_parametrs, function_call, function_parametrs_declaration;
non terminal Opr   expr_list;
non terminal Opr       statement_list, statement;
non terminal Opr   function_body;
non terminal Opr     function;
non terminal Opr       factor, term;
non terminal Opr       expr;
non terminal String     programm_name; // other string non terminals
non terminal Opr defenition_value, defenition_const_value, defenition_array_of, defenition_const_array_of, defenition_pointer, defeniniton_const_pointer; // defenition
non terminal boolean    const;
terminal     Opr        RETURN;
/* -------------Precedence and Associatively of Terminals Section----------- */

precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE;


   programm ::= programm function  {: RESULT = new Program(); :}
                | function
                ;
   function ::= type:t NAME:n  LPAREN function_parametrs_declaration:fp RPAREN LBRACE function_body:b RBRACE // объявление функции
                {: context.registerFunction(new FunctionDefinition(t, n, fp, b)); :}
                ;
   function_parametrs_declaration ::= initialize:i  function_parametrs_declaration:fpd {:
                Opr res = new Opr(NodeType.VAR, operType.NEXTSTMT);
                res.addInListOpr(i);
                if (fpd != null)
                    res.addInListOpr(fpd);
                RESULT = res; :}
                | COMMA initialize:i function_parametrs_declaration:fpd {: Opr res = new Opr(NodeType.VAR, operType.NEXTSTMT);
                res.addInListOpr(i);
                if (fpd != null)
                    res.addInListOpr(fpd);
                RESULT = res; :}
                |
                ;
   function_body ::= // тело функции
                statement:s function_body:b {:
                Opr res = new Opr(NodeType.OPR, operType.NEXTSTMT);
                res.addInListOpr(s);
                res.addInListOpr(b);
                RESULT = res; :}
                | statement:s {: Opr res = new Opr(NodeType.OPR);
                res.addInListOpr(s);
                RESULT = res; :}
                ;
   statement    ::=
                RETURN expr:n SEMI {:
                if (n instanceof Variable) {
                    Opr res = new Opr(NodeType.OPR, operType.RETURN);
                    Variable var = context.getVar(((Variable) n).getName());
                    res.addInListOpr(var);
                    RESULT = res;
                    } else {
                    RESULT = n;
                }
                :}
                | SEMI {: Opr res = new Opr(NodeType.OPR, operType.SEMI);
                 res.addInListOpr(null);
                 RESULT = res;
                 :}
                | NAME:n ASSIGN expr:e SEMI {: Opr res = new Opr(NodeType.OPR, operType.ASSIGN);
                Variable var = context.getVar(n);
                res.addInListOpr(var);
                res.addInListOpr(e);
                RESULT = res;
                 :}
                | NAME:n LSQUARE expr:i RSQUARE ASSIGN expr:e SEMI {: Opr res = new Opr(NodeType.OPR, operType.ASSIGN);
                Variable var = context.getVar(n);
                res.addInListOpr(var);
                res.addInListOpr(i);
                res.addInListOpr(e);
                RESULT = res;
                :}
                | WHILE LPAREN expr:e RPAREN LBRACE function_body:s RBRACE {:
                Opr res = new Opr(NodeType.OPR, operType.WHILE);
                res.addInListOpr(e);
                res.addInListOpr(s);
                RESULT = res;
                :}
                | function_call:fc {: RESULT = fc; :}
                | initialize:i SEMI {: RESULT = i; :}
                | expr_list SEMI
                ;
   function_parametrs ::=
                NAME:n function_parametrs:fp // один или несколько параметров
                {: Opr res = new Opr(NodeType.VAR);
                Variable var = context.getVar(n);
                res.addInListOpr(var);
                if (fp != null)
                    res.addInListOpr(fp);
                RESULT = res;
                :}
                | COMMA NAME:n function_parametrs:fp
                {:
                Variable var = context.getVar(n);
                RESULT = var;
                Opr res = new Opr(NodeType.VAR);
                res.addInListOpr(var);
                if (fp != null)
                    res.addInListOpr(fp);
                RESULT = res;
                :}
                |
                ;
   function_call ::=  NAME:n LPAREN {: context.newScope(); :} function_parametrs:p RPAREN
                {:
                Opr funcParams = context.funcParametrs(n);
                Opr res = new Opr(NodeType.OPR, operType.FUNC_CALL);
                res.addInListOpr(context.rootFunc(n));
                res.addInListOpr(funcParams);
                res.addInListOpr(p);
                RESULT = res;
                context.deleteScope();
                :}
                ;
   type ::=     const_value:v {: :}
                |
                value:v {: RESULT = v; :}
                |
                const_pointer:p  // указатель
                |
                pointer:p {:  :}
                |
                const_array_of:a // массив
                |
                array_of:t {: RESULT = t; :}
                ;
   initialize ::= defenition_value:e ASSIGN expr:n
                     {: Opr res = new Opr(NodeType.OPR, operType.ASSIGN);
                      res.addInListOpr(e);
                      res.addInListOpr(n);
                      RESULT = res;
                      :}
                     | defenition_value:e {: RESULT = e; :}
                     | defenition_pointer:p {: RESULT = p; :}
                     | defeniniton_const_pointer:p
                     | defenition_array_of:a {: RESULT = a; :}
                     | defenition_const_array_of:a
                     | defenition_pointer:p ASSIGN expr:e {: Opr res = new Opr(NodeType.OPR, operType.ASSIGN);
                      res.addInListOpr(p);
                      res.addInListOpr(e);
                      RESULT = res;:}
                     ;
   defenition_value ::= value NAME:n
                     {:
                     Variable var = new Variable(Types.VALUE, n);
                     context.addVar(var);
                     var.setValue(new Const(0));
                     RESULT = var;
                     :}
                     ;
   defenition_pointer ::= POINTER:p type:t NAME:n
                    {: Variable var = new Variable(Types.POINTER, n);
                     context.addVar(var);
                     var.setValue(new Variable(t, n));
                     RESULT = var; :}
                    ;
   defenition_array_of ::= array_of:t NAME:n
                    {: Variable var = new Variable(Types.ARRAY_OF, n);
                    context.addVar(var);
                    Opr res = new Opr(NodeType.VAR);
                    res.addInListOpr(var);
                    res.addInListOpr(new Const(4));
                    RESULT = res;
                    :}
                    |
                    array_of:t NAME:n LSQUARE expr:e RSQUARE
                    {: Variable var = new Variable(Types.ARRAY_OF, n);
                    context.addVar(var);
                    Opr res = new Opr(NodeType.VAR);
                    res.addInListOpr(var);
                    res.addInListOpr(e);
                    RESULT = res;
                    :}
                    ;
   expr      ::=  function_call:fc {: RESULT = fc; :}
                 | expr:e LTE factor:f {: Opr res = new Opr(NodeType.OPR, operType.LTE);
                  res.addInListOpr(e);
                  res.addInListOpr(f);
                  RESULT = res; :}
                 | expr:e GTE factor:f {: Opr res = new Opr(NodeType.OPR, operType.GTE);
                  res.addInListOpr(e);
                  res.addInListOpr(f);
                  RESULT = res; :}
                 | expr:e NE factor:f {: Opr res = new Opr(NodeType.OPR, operType.NE);
                 res.addInListOpr(e);
                 res.addInListOpr(f);
                 RESULT = res;
                 :}
                 | expr:e PLUS factor:f
                 {:
                  Opr res = new Opr(NodeType.OPR, operType.PLUS);
                  res.addInListOpr(e);
                  res.addInListOpr(f);
                  RESULT = res;:}
                 | expr:e MINUS factor:f
                 {: Opr res = new Opr(NodeType.OPR, operType.MINUS);
                  res.addInListOpr(e);
                  res.addInListOpr(f);
                  RESULT = res; :}
                 | factor:f
                 {: RESULT = f; :}
                 ;
   factor    ::= factor:f TIMES term:t
                 {: Opr res = new Opr(NodeType.OPR, operType.TIMES);
                  res.addInListOpr(f);
                  res.addInListOpr(t);
                  RESULT = res;
                  :}
                 |
                 factor:f DIVIDE term:t
                 {:
                 Opr res = new Opr(NodeType.OPR, operType.DIVIDE);
                 res.addInListOpr(f);
                 res.addInListOpr(t);
                 RESULT = res;
                  :}
                 | factor:f MOD term:t
                 {:
                 Opr res = new Opr(NodeType.OPR, operType.MOD);
                 res.addInListOpr(f);
                 res.addInListOpr(t);
                 RESULT = res;
                 :}
                 | term:t
                 {: RESULT = t; :}
                 ;
   value ::= VALUE {: RESULT = Types.VALUE; :};
   const_value ::= CONST VALUE {: RESULT = Types.CONST_VALUE; :};
   pointer ::= POINTER type:t {: RESULT = Types.POINTER; RESULT.setType(t); :};
   const_pointer ::= CONST POINTER type:t {: RESULT = Types.CONST_POINTER; RESULT.setType(t); :} ;
   array_of ::= ARRAY_OF type:t {: RESULT = t; :} ;
   const_array_of ::= CONST ARRAY_OF type;
   term      ::= LPAREN expr:e RPAREN
                 {: RESULT = e; :}
                 |
                 NUMBER:n
                 {: RESULT = (Opr) new Const(n);
                 :}
                 |
                 NAME:n {: Variable var = context.getVar(n);
                 RESULT = var; :}
                 | NAME:n LSQUARE expr:e RSQUARE {: Variable var = context.getVar(n);
                 Opr res = new Opr(NodeType.OPR, operType.TAKE_FROM_ARRAY);
                 res.addInListOpr(var);
                 res.addInListOpr(e);
                 RESULT = res;
                 :}
                 ;